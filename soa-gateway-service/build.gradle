plugins {
    id 'org.springframework.boot' version '3.1.0'
    id 'uk.co.boothen.gradle.wsimport' version '0.19'
    id 'jacoco'
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    integrationTestImplementation.extendsFrom(testImplementation)
    integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
}

dependencies {
    implementation project(':soa-gateway-api')

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.ws:spring-ws-security'
    implementation 'org.modelmapper:modelmapper:3.1.1'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-core:3.12.4'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
}

wsimport {
    extension = true
    verbose = false
    quiet = true
    debug = false
    xadditionalHeaders = true

    wsdl('Schema/BusinessServices/CCMS/CaseManagement/NotificationServices.wsdl') {
        packageName = 'uk.gov.laa.ccms.soa.gateway.notificationservice'
        xjcarg("-XautoNameResolution")
        wsdlLocation = 'classpath:wsdl/Schema/BusinessServices/CCMS/CaseManagement/NotificationServices.wsdl'
    }
}

test {
    useJUnitPlatform()
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    description = "Run integration tests"
    group = "verification"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }

    dependsOn tasks['test']
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80  // You can define your required coverage ratio here.
            }
        }
    }

    dependsOn tasks['test']
}


