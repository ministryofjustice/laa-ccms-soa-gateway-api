plugins {
    id 'uk.co.boothen.gradle.wsimport' version '0.22'
}

apply plugin: 'uk.gov.laa.ccms.springboot.laa-ccms-spring-boot-gradle-plugin'

dependencies {

    constraints {
        implementation('org.bouncycastle:bcprov-jdk15on:1.70') {
            because("Fixes CVE-2018-1000613")
        }
        implementation("org.springframework.security:spring-security-core:6.5.0") {
            because("Fixes CVE-2025-41232")
        }
    }

    implementation project(':soa-gateway-api')

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'

    implementation 'org.springframework.boot:spring-boot-starter-validation'

    //Metrics
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    //Enable access token authentication
    implementation 'uk.gov.laa.ccms.springboot:laa-ccms-spring-boot-starter-auth'

    //Enable Swagger UI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'

    //Required for pagination
    implementation 'org.springframework.data:spring-data-commons'

    // For mock Contract Details only
    // Prefer standalone version, see: https://github.com/wiremock/wiremock/issues/1760
    implementation 'org.wiremock:wiremock-standalone:3.13.0'

    // Exclude guava from spring-ws-security
    implementation('org.springframework.ws:spring-ws-security') {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'org.springframework.security', module: 'spring-security-core'
    }
    // Overriding transitive dependency
    implementation 'com.google.guava:guava:33.4.8-jre'
    implementation 'org.modelmapper:modelmapper:3.2.3'
    implementation 'org.apache.commons:commons-lang3:3.17.0'
    implementation "org.mapstruct:mapstruct:1.6.3"
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.mapstruct:mapstruct-processor:1.6.3"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    // Adding exclusion for legacy JUnit 4
    testImplementation('org.springframework.ws:spring-ws-test') {
        exclude group: 'junit', module: 'junit'
    }
    testImplementation 'org.mockito:mockito-core:5.18.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.0'


}

wsimport {
    extension = true
    verbose = false
    quiet = true
    debug = false
    xadditionalHeaders = true

    wsdl('Schema/BusinessServices/CCMS/CaseManagement/NotificationServices.wsdl') {
        xjcarg("-XautoNameResolution")
        wsdlLocation = 'classpath:wsdl/Schema/BusinessServices/CCMS/CaseManagement/NotificationServices.wsdl'
    }

    wsdl('/Schema/BusinessServices/CCMS/Common/GetContractDetails.wsdl') {
        xjcarg("-XautoNameResolution")
        wsdlLocation = 'classpath:wsdl/Schema/BusinessServices/CCMS/Common/GetContractDetails.wsdl'
    }

    wsdl('/Schema/BusinessServices/CCMS/ClientManagement/ClientServices.wsdl') {
        xjcarg("-XautoNameResolution")
        wsdlLocation = 'classpath:wsdl/Schema/BusinessServices/CCMS/ClientManagement/ClientServices.wsdl'
    }

    wsdl('/Schema/BusinessServices/CCMS/Common/GetReferencedata.wsdl') {
        xjcarg("-XautoNameResolution")
        wsdlLocation = 'classpath:wsdl/Schema/BusinessServices/CCMS/Common/GetReferencedata.wsdl'
    }

    wsdl('Schema/BusinessServices/CCMS/CaseManagement/CaseServices.wsdl') {
        xjcarg("-XautoNameResolution")
        wsdlLocation = 'classpath:wsdl/Schema/BusinessServices/CCMS/CaseManagement/CaseServices.wsdl'
    }

    wsdl('Schema/BusinessServices/CCMS/CaseManagement/DocumentServices.wsdl') {
        xjcarg("-XautoNameResolution")
        wsdlLocation = 'classpath:wsdl/Schema/BusinessServices/CCMS/CaseManagement/DocumentServices.wsdl'
    }

    wsdl('Schema/BusinessServices/CCMS/CaseManagement/GetCoverSheet.wsdl') {
        xjcarg("-XautoNameResolution")
        wsdlLocation = 'classpath:wsdl/Schema/BusinessServices/CCMS/CaseManagement/GetCoverSheet.wsdl'
    }

    wsdl('Schema/BusinessServices/CCMS/UserManagement/UpdateUser.wsdl') {
        xjcarg("-XautoNameResolution")
        wsdlLocation = 'classpath:wsdl/Schema/BusinessServices/CCMS/UserManagement/UpdateUser.wsdl'
    }

    wsdl('Schema/BusinessServices/CCMS/CaseManagement/SubmitProviderRequest.wsdl') {
        xjcarg("-XautoNameResolution")
        wsdlLocation = 'classpath:wsdl/Schema/BusinessServices/CCMS/CaseManagement/SubmitProviderRequest.wsdl'
    }
}

test {
    useJUnitPlatform()

    // Hide warning for dynamic loading of agents https://github.com/mockito/mockito/issues/3037
    jvmArgs '-XX:+EnableDynamicAgentLoading'

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/SoaGatewayApplication.class',
                                        'org/oasis_open/docs/wss/**',
                                        'uk/gov/legalservices/**',
                                        'uk/gov/gsi/legalaid/**'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/SoaGatewayApplication.class',
                                        'org/oasis_open/docs/wss/**',
                                        'uk/gov/legalservices/**',
                                        'uk/gov/gsi/legalaid/**'
            ])
        }))
    }
}

checkstyleMain {
    //exclude auto generated files
    exclude '**/uk/gov/legalservices/**'
    exclude '**/uk/gov/gsi/legalaid/**'
    exclude '**/org/oasis_open/**'
}
